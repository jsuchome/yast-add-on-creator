/* -----------------------------------------------------------------------------
 * Copyright (c) 2006-2012 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * -----------------------------------------------------------------------------
 */

/**
 * File:	include/add-on-creator/wizards.ycp
 * Package:	Configuration of add-on
 * Summary:	Wizards definitions
 * Authors:	Jiri Suchomel <jsuchome@suse.cz>
 *
 * $Id$
 */

{

textdomain "add-on-creator";

import "Popup";
import "Sequencer";
import "Wizard";

include "add-on-creator/complex.ycp";
include "add-on-creator/dialogs.ycp";
include "add-on-creator/patterns.ycp";

// fill the defaults for content file
any GenerateContent () {

    AddOnCreator::FillContentDefaults ();
    return `next;
}

// import data from existing product
any CopyExisting () {

    // busy message
    Popup::ShowFeedback ("", _("Importing product..."));

    AddOnCreator::ImportExistingProduct (AddOnCreator::import_path);

    Popup::ClearFeedback ();
    return `next;
}

// save the data with current configuration into global list
any Commit () {

    AddOnCreator::CommitCurrentProduct ();
    return `next;
}

// if there are no Add-On products configured, open creation sequence
any FirstDialog () {
    if (size (AddOnCreator::add_on_products) > 0)
    {
	return `summary;
    }
    AddOnCreator::selected_product	= -1;
    return `new;
}

/**
 * Main workflow of the add-on-creator configuration: create or edit Add-On
 * @return sequence result
 */
any MainSequence() {

    map aliases = $[
	"new"		:   ``( NewProductDialog() ),
	"content"	:   ``( ContentFileDialog() ),
	"sources"	:   ``( SourcesDialog() ),
	"expert1"	:   ``( ExpertSettingsDialog() ),
	"expert2"	:   ``( ExpertSettingsDialog2() ),
	"packages"	:   ``( PackagesDialog() ),
	"patterns"	:   ``( PatternsDialog() ),
	"signing"	:   ``( SigningDialog() ),
	"output"	:   ``( OutputDialog() ),
	"overview"	:   ``( OverviewDialog() ),
	"workflow"	:   ``( WorkflowConfigurationDialog() ),
	"copy"		:  [ ``(CopyExisting ()), true ],
	"generate"	:  [ ``(GenerateContent ()), true ],
	"commit"	:   ``( Commit ()),
    ];
    string start_dialog	= (AddOnCreator::selected_product == -1) ?
	"new" : "sources";
    map sequence = $[
	"ws_start"	: start_dialog,
	"new" : $[
	    `abort	: `abort,
	    `next	: "sources",
	    `copy	: "copy",
	],
	"sources" : $[
	    `abort	: `abort,
	    `next	: "generate",
	    `skip_gen	: "content",
	],
	"copy"	: $[
	    `next	: "generate",
	],
	"generate"	: $[
	    `next	: "content",
	],
	"content" : $[
	    `abort	: `abort,
	    `next	: "packages",
	],
	"packages" : $[
	    `abort	: `abort,
	    `next	: "patterns",
	],
	"patterns" : $[
	    `abort	: `abort,
	    `next	: "output",
	],
	"output" : $[
	    `abort	: `abort,
	    `next	: "signing",
	    `expert	: "expert1",
	    `workflow	: "workflow",
	],
	"workflow" : $[
	    `abort	: `abort,
	    `next	: "output",
	],
	"signing" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"overview" : $[
	    `abort	: `abort,
//	    `next	: `next,
	    `next	: "commit",
	],
	"commit" : $[
	    `next	: `next,
//	    `next	: "summary",
	],
	"expert1"	: $[
	    `abort	: `abort,
	    `next	: "expert2",
	],
	"expert2"	: $[
	    `abort	: `abort,
	    `next	: "output",
	],
    ];

    any ret = Sequencer::Run(aliases, sequence);

    return ret;
}

/**
 * Whole configuration of add-on-creator
 * @return sequence result
 */
any AddOnCreatorSequence() {

    map aliases = $[
	"read"		: [ ``( ReadDialog() ), true ],
	"start"		: [ ``( FirstDialog ()), true ],
	"summary"	:   ``( SummaryDialog() ),
	"build"		:   ``( BuildDialog ()),
	"main"		:   ``( MainSequence() ),
	"write"		: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "start"
	],
	"start" : $[
	    `summary	: "summary",
	    `new	: "main",
	],
	"summary" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `new	: "main",
	    `edit	: "main",
	    `build	: "build",
	    `next	: "write",
	],
	"build"		: $[
	    `next	: "summary",
	    `abort	: "summary",
	],
	"main" : $[
	    `abort	: "summary",
	    `next	: "summary",
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();

    any ret = Sequencer::Run(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/* EOF */
}
